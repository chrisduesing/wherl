#!/usr/bin/env escript
-export([main/1]).

-include_lib("kernel/include/file.hrl").

main(Something) when is_list(Something) ->
    [Command | Args] = Something,
    case Command of
	"new" ->
	    handle(Command, Args);
	"help" ->
	    handle(Command, Command);
	_ ->
	    case app_created() of
		true ->
		    handle(Command, Args);
		false ->
		    io:format("You must create an app before you can do anything else.~n\tusage: wherl new <name> <abbreviation>~n",[])
	    end
    end;	

main(_) ->
    io:format("usage: wherl generate <command> <argument>. try wherl help for a list of commands.~n",[]).	

%%%===================================================================
%%% API
%%%===================================================================
handle("new", Args) ->
    case (length(Args)) of
	1 ->
	    [App] = Args,
	    new_app(App, "");
	2 ->  
	    [App | Rest] = Args,
	    [Abbrev] = Rest,
	    new_app(App, Abbrev);
	_ ->
	    io:format("usage: wherl new <name> <abbreviation>~n \tname: name of the application~n \tabbreviation: since erlang is not namespaced, a short (1-4 character) prefix can be appended to each module to avoid name clashes.~n",[]) %
    end;

handle("generate", ["controller" | Rest]) ->
    case (length(Rest) == 1) of
	true ->	  
	    io:format("~ngenerating controller ~s~n",[Rest]),
	    controller(Rest);
	false ->	
	    io:format("usage: wherl generate controller <name>~n",[])
    end;

handle("generate", ["model" | Rest]) ->
    case (length(Rest) == 0) of
	true ->
	    io:format("usage: wherl generate model <name>~n",[]);
	false ->	
	    [Name | Fields] = Rest,
	    io:format("~ngenerating model ~s~n",[Name]),
	    model(Name, Fields)
    end;

handle("generate", ["scaffold" | Rest]) ->
    case (length(Rest) == 1) of
	true ->
	    Name = string:strip(Rest),
	    io:format("generating model and controller ~s~n",[Name]);
	false ->	
	    io:format("usage: wherl generate <command> <args>~n",[])
    end;

handle(_, _) ->
    io:format("usage: wherl <command> <args>~n \tcommands:~n \t\tnew - create a new application~n \t\tgenerate - make a new model or controller~n",[]).

%%%===================================================================
%%% Template generation
%%%===================================================================
new_app(App, Abbrev) ->
    io:format("~nwherl: creating new app ~s~n",[App]),
    config(App, Abbrev, []),
    create_dir(App), 
    create_dir(App ++ "/db"),
    create_dir(App ++ "/priv/www"),
    rebar_create(App, App),
    supervisor(App),
    rebar_deps(App),
    hrl(App),
    util(App),
    session(App),
    resource(App),
    dispatch(App),
    static(App),
    dev(App),
    rebar_compile(),
    start_script(App).

config(App, Abbrev, Models) ->
    {ok, ConfigFile} = create_file("config/wherl.config"),
    file:write(ConfigFile, io_lib:fwrite("~p.\n",[{app, App}])),
    file:write(ConfigFile, io_lib:fwrite("~p.\n",[{abbrev, Abbrev}])),
    file:write(ConfigFile, io_lib:fwrite("~p.\n",[{models, Models}])),
    file:close(ConfigFile).

rebar_create(App, AppDir) ->
    os:cmd("cp rebar " ++ AppDir),
    Cmd = "cd " ++ AppDir ++ "; ./rebar create-app appid=" ++ App,
    io:format("rebar:~n~s~n", [os:cmd(Cmd)]).

supervisor(App) ->
    TemplateFileName = "supervisor.tmp",
    Data = template(TemplateFileName, [{app, App}]),
    FileName = lists:flatten([App, "/src/", App, "_sup.erl"]),
    {ok, File} = create_file(FileName),
    file:write(File, Data).

rebar_deps(App) ->
    io:format("Fetching dependencies from teh interwebz, this may take a moment...~n", []),
    os:cmd("cp templates/rebar.config " ++ App),
    Cmd = "cd " ++ App ++ "; ./rebar get-deps",
    io:format("rebar:~n~s~n", [os:cmd(Cmd)]).

hrl(App) ->
    TemplateFileName = "hrl.tmp",
    Data = template(TemplateFileName, [{app, App}]),
    FileName = lists:flatten([App, "/include/", App, ".hrl"]),
    {ok, HrlFile} = create_file(FileName),
    file:write(HrlFile, Data).
    
model(Model, Fields) ->
    Properties = model_properties(Model, Fields),
    TemplateFileName = "model.tmp",
    Data = template(TemplateFileName, Properties),
    FileName = lists:flatten([app_name(), "/src/", Model, ".erl"]),
    {ok, ModelFile} = create_file(FileName),
    file:write(ModelFile, Data),
    {App, Abbrev, Models} = app_props(),
    config(App, Abbrev, [Model | Models]),
    wh_record(Properties),
    store(Model, Properties),
    rebar_compile().

controller(Controller) ->
    Properties = controller_properties(Controller),
    TemplateFileName = "controller.tmp",
    Data = template(TemplateFileName, Properties),
    FileName = lists:flatten([app_name(), "/src/", Controller, "_controller.erl"]),
    {ok, ControllerFile} = create_file(FileName),
    file:write(ControllerFile, Data).
%%% Template Insertion Point - Do not delete this line!

wh_record(Properties) ->
    App = app_name(),
    TemplateFileName = "record.tmp",
    Data = template(TemplateFileName, Properties),
    HrlFileName = lists:flatten([App, "/include/", App, ".hrl"]),
    {ok, HrlFile} = create_file(HrlFileName, [write, append]),
    file:write(HrlFile, Data).

store(Model, Properties) ->
    TemplateFileName = "store.tmp",
    Data = template(TemplateFileName, Properties),
    FileName = lists:flatten([app_name(), "/src/", Model, "_store.erl"]),
    {ok, File} = create_file(FileName),
    file:write(File, Data),
    store_sup().

util(App) ->
    Properties = [{app, App}],
    TemplateFileName = "util.tmp",
    Data = template(TemplateFileName, Properties),
    FileName = lists:flatten([App, "/src/", App, "_util.erl"]),
    {ok, File} = create_file(FileName),
    file:write(File, Data).

store_sup() ->
    {_App, _Abbrev, Models} = app_props(),
    Properties = model_sup_properties(Models),
    TemplateFileName = "store_sup.tmp",
    Data = template(TemplateFileName, Properties),
    ChildSpecs = lists:foldl(fun(M, CS) -> CS ++ store_sup_child(M) end, "", Models),
    Data2 = replace(Data, "%%% child_specs", ChildSpecs),
    FileName = lists:flatten([app_name(), "/src/store_sup.erl"]),
    {ok, SupFile} = create_file(FileName),
    file:write(SupFile, Data2).

store_sup_child(Model) ->
    TemplateFileName = "store_sup_model.tmp",
    template(TemplateFileName, [{model, Model}, {'Model', title(Model)}]).

session(App) ->
    TemplateFileName = "wherl_session_store.tmp",
    Data = template(TemplateFileName, [{app, App}]),
    FileName = lists:flatten([App, "/src/wherl_session_store.erl"]),
    {ok, File} = create_file(FileName),
    file:write(File, Data).

dispatch(App) ->
    os:cmd(lists:flatten(["cp templates/dispatch.conf ", App, "/priv/dispatch.conf"])).

resource(App) ->
    TemplateFileName = "wherl_resource.tmp",
    Data = template(TemplateFileName, [{app, App}]),
    FileName = lists:flatten([App, "/src/wherl_resource.erl"]),
    {ok, File} = create_file(FileName),
    file:write(File, Data).

static(App) ->
    TemplateFileName = "static_resource.tmp",
    Data = template(TemplateFileName, [{app, App}]),
    FileName = lists:flatten([App, "/src/static_resource.erl"]),
    {ok, File} = create_file(FileName),
    file:write(File, Data),
    TemplateFileName2 = "index.tmp",
    Data2 = template(TemplateFileName2, [{app, App}]),
    FileName2 = lists:flatten([App, "/priv/www/index.html"]),
    {ok, File2} = create_file(FileName2),
    file:write(File2, Data2).

dev(App) ->
    TemplateFileName = "app.tmp",
    Data = template(TemplateFileName, [{app, App}]),
    FileName = lists:flatten([App, "/src/", App, ".erl"]),
    {ok, File} = create_file(FileName),
    file:write(File, Data).

start_script(App) ->
    TemplateFileName = "start.tmp",
    {ok, CurrentDirectory} = file:get_cwd(),
    AppDir = lists:flatten([CurrentDirectory, "/", App]),
    Data = template(TemplateFileName, [{app, App}, {app_dir, AppDir}]),
    FileName = lists:flatten([App, "/start.sh"]),
    {ok, File} = create_file(FileName),
    file:write(File, Data),
    os:cmd(lists:flatten(["chmod +x ", App, "/start.sh"])).


%%%===================================================================
%%% Utility functions
%%%===================================================================


template(TemplateFileName, Properties) ->
    CurrentDirectory = filename:dirname(escript:script_name()),
    true = code:add_pathz(CurrentDirectory ++ "/include/sgte/ebin/"),
    FileName = lists:flatten([CurrentDirectory, "/templates/", TemplateFileName]),
    {ok, Compiled} = sgte:compile_file(FileName),
    sgte:render_bin(Compiled, Properties).

create_file(FileNameAndPath) ->
    create_file(FileNameAndPath, [write]).

create_file(FileNameAndPath, Options) ->
    Index = string:rchr(FileNameAndPath, $/),
    Path = string:substr(FileNameAndPath, 1, Index - 1),
    create_dir(Path),
    CurrentDirectory = filename:dirname(escript:script_name()),
    FullFileName = lists:flatten([CurrentDirectory, "/", FileNameAndPath]),
    {ok, File} = file:open(FullFileName, Options),
    io:format("writing file:  ~s~n",[FullFileName]),
    {ok, File}.

create_dir(Path) ->
    CurrentDirectory = filename:dirname(escript:script_name()),
    lists:foldl(fun(Directory, PathSoFar) -> 
			NewDir = lists:flatten([PathSoFar, "/", Directory]),
			file:make_dir(CurrentDirectory ++ NewDir), 		
			NewDir
		end, "", string:tokens(Path, "/ ")).

app_name() ->
    PropList = app_raw_proplist(),
    {app, AppName} = lists:keyfind(app, 1, PropList),
    AppName.

app_props() ->
    PropList = app_raw_proplist(),
    {app, App} = lists:keyfind(app, 1, PropList),
    {abbrev, Abbrev} = lists:keyfind(abbrev, 1, PropList),
    {models, Models} = lists:keyfind(models, 1, PropList),
    {App, Abbrev, Models}.

app_raw_proplist() ->
    CurrentDirectory = filename:dirname(escript:script_name()),
    Filename = CurrentDirectory ++ "/config/wherl.config",
    {ok, PropList} = file:consult(Filename),
    PropList.

app_created() ->
    CurrentDirectory = filename:dirname(escript:script_name()),
    Filename = CurrentDirectory ++ "/config/wherl.config",
    {Result, _} = file:read_file_info(Filename),
    Result == ok.

rebar_compile() ->
    Cmd = "cd " ++ app_name() ++ "; ./rebar compile",
    io:format("rebar:~n~s~n", [os:cmd(Cmd)]).

model_properties(Model, Fields) ->
    FieldTitleList = string:sub_string(lists:flatten([ ", " ++ title(F) || F <- Fields ]), 3),
    FieldRecordList = string:sub_string(lists:flatten([ ", " ++ F ++ "=" ++ title(F) || F <- Fields ]), 3),
    FieldList = string:sub_string(lists:flatten([ ", " ++ F || F <- Fields ]), 3),
    [{model, Model}, 
     {'Model', title(Model)},
     {app, app_name()},
     {field_count, length(Fields)},
     {field_title_list, FieldTitleList},
     {field_list, FieldList},
     {field_record_list, FieldRecordList},
     {abbrev, ""}].

model_sup_properties(Models) ->
    ChildList = string:sub_string(lists:flatten([ ", " ++ title(M) || M <- Models ]), 3),
    [{models, Models}, 
     {app, app_name()},
     {child_list, ChildList}].

controller_properties(Controller) ->
    [{controller, Controller}, 
     {'Controller', title(Controller)},
     {app, app_name()}].

title([H|T]) -> 
    [string:to_upper(H)| T].

replace(Original, Key, Data) ->
    re:replace(Original, Key, Data).
